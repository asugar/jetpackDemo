apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'dellog'
apply from: '../groovy_base.gradle'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "com.yi.jetpackdemo"
        minSdkVersion 17
        targetSdkVersion 29
        versionCode verCode as Integer
        versionName getLocalVersionName()
//        archivesBaseName = "jetpack-$versionName"
        multiDexEnabled true
    }

    signingConfigs {
        config {
            keyAlias 'xiaoyi'
            keyPassword '123456'
            storeFile file('sign.jks')
            storePassword '123456'
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }

    }
    // 用于android应用gradle插件 libraryVariants  testVariants
    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "jetpack_${variant.buildType.name}_${defaultConfig.versionName}.apk"
        }
    }
//    applicationVariants.all { variant ->
//        variant.outputs.each { output ->
//            if (output.outputFile != null && output.outputFile.name.endsWith('.apk') && "release".equals(variant.buildType.name)) {
//                def apkFile = new File(output.outputFile.getParent(), "2jetpack_${variant.buildType.name}_${defaultConfig.versionName}")
//                output.outputFile = apkFile
//            }
//        }
//    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    // 使用buildFeature声明
    buildFeatures {
        viewBinding = true
        dataBinding = true
    }
    dynamicFeatures = [':rxjavaDemo']

}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.3.0'

    // Kotlin concurrent-futures
    implementation "androidx.concurrent:concurrent-futures-ktx:1.1.0-alpha01"

    // ViewModel
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    // LiveData
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    // fragment navigation
    implementation 'androidx.navigation:navigation-fragment-ktx:2.3.0'
    implementation 'androidx.navigation:navigation-ui-ktx:2.3.0'
    // Annotation processor
    kapt "androidx.lifecycle:lifecycle-compiler:$lifecycle_version"
    // alternately - if using Java8, use the following instead of lifecycle-compiler
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"

    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    // 添加wrokManager
    def work_version = "2.3.4"
    // (Java only)
    implementation "androidx.work:work-runtime:$work_version"
    // Kotlin + coroutines
    implementation "androidx.work:work-runtime-ktx:$work_version"
    // optional - RxJava2 support
    implementation "androidx.work:work-rxjava2:$work_version"
    // optional - GCMNetworkManager support
    implementation "androidx.work:work-gcm:$work_version"

    // 添加room持久库
    def room_version = "2.2.5"
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    // For Kotlin use kapt instead of annotationProcessor
    // optional - Kotlin Extensions and Coroutines support for Room
    implementation "androidx.room:room-ktx:$room_version"
    // optional - RxJava support for Room
    implementation "androidx.room:room-rxjava2:$room_version"
    // optional - Guava support for Room, including Optional and ListenableFuture
    implementation "androidx.room:room-guava:$room_version"
    // Test helpers
    testImplementation "androidx.room:room-testing:$room_version"

    // 添加stecho库 查看数据库，打印网络日志
    implementation 'com.facebook.stetho:stetho:1.5.1'
    implementation 'com.facebook.stetho:stetho-okhttp3:1.5.1'
    implementation 'com.facebook.stetho:stetho-js-rhino:1.5.1'

    // multidex
    def multidex_version = "2.0.1"
    implementation "androidx.multidex:multidex:$multidex_version"

    // logger
    implementation "com.orhanobut:logger:2.2.0"

    // smarttablayout
    implementation 'com.ogaclejapan.smarttablayout:library:2.0.0@aar'
    implementation 'com.ogaclejapan.smarttablayout:utils-v4:2.0.0@aar'


    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

}

static def generateApkTime() {
    return new Date().format("yyyyMMddHHmm", TimeZone.getTimeZone("GMT+8"))
}

def getLocalVersionName() {
    def vName
    if (!gradle.startParameter.taskNames.contains(":app:release")) {
        vName = verName + "_" + generateApkTime()
    } else {
        vName = verName
    }
    println("versionName1=" + gradle.startParameter.taskNames)
    println("versionName2=" + vName)
    return vName
}

// 自定义plugin
//dellogExtension.sourceDir = '/src'
dellogExtension {
//    sourceDir = '/src'
}
